Player.AuxVarSetFlt "*_ARsoutog" (GetINIFloat "AlertSound:fAlertSoundToggle" "AlertReaction.ini")
Player.AuxVarSetFlt "*_ARslomotog" (GetINIFloat "SlowMotion:fSlowMotionToggle" "AlertReaction.ini")
Player.AuxVarSetFlt "*_ARslomosca" (GetINIFloat "SlowMotion:fSlowMotionTimeScale" "AlertReaction.ini")
Player.AuxVarSetFlt "*_ARslomodur" (GetINIFloat "SlowMotion:fSlowMotionDuration" "AlertReaction.ini")
Player.AuxVarSetFlt "*_ARslomopls" (GetINIFloat "SlowMotion:fSlowMotionEnemyNumGain" "AlertReaction.ini")
Player.AuxVarSetFlt "*_ARappalert" 0
Player.AuxVarSetFlt "*_ARnumofenm" 0

SetEventHandlerAlt "OnStartCombat" (begin function {ref rTrt, ref rAtk}
	print "Combat Start"
	if Player.GetDistance3D rTrt <= 3500 && player.IsSneaking
		Player.AuxVarSetFlt "*_ARnumofenm" (Ar_Size Player.GetCombatTargets)
		Player.AuxVarSetRef "*_ARcombatenm" rTrt
		print "Enemy number:" + (tostring Player.AuxVarGetFlt "*_ARnumofenm")
	endif
end) 2::player

SetEventHandlerAlt "OnCombatEnd" (begin function {ref rTgt, ref rEdr}
	CallAfterFrames 5 (begin function {}
		if eval !(Player.IsInCombat) && !((Player.AuxVarGetRef "*_ARcombatenm").IsLimbCrippled 6)
			Player.AuxVarSetFlt "*_ARappalert" 0
			Player.AuxVarSetFlt "*_ARnumofenm" 0
			sgtm 1.0
			print "Cleared"
		endif
	end)
end)

SetEventHandlerAlt "AlertReactionStart" (begin function {}
	Player.AuxVarSetFlt "*_ARappalert" 1
	if Player.AuxVarGetFlt "*_ARsoutog"
		PlaySoundFile "Data/sound/fx/AlertReaction/AlertSound.mp3" 1 0 0
		print "Play sound"
	endif
	if Player.AuxVarGetFlt "*_ARslomotog"
		DispatchEventAlt "AlertReactionSlowMo"
		print "Slowmo start"
	endif
end)

; SlowMo Time Calculation : Duration * Timescale + EnemyNumGain * (EnemyNumber - 1)
SetEventHandlerAlt "AlertReactionSlowMo" (begin function {}
	if ggtm == 1.0
		sgtm (Player.AuxVarGetFlt "*_ARslomosca")
		CallAfterSeconds ((Player.AuxVarGetFlt "*_ARslomodur") * (Player.AuxVarGetFlt "*_ARslomosca") + (Player.AuxVarGetFlt "*_ARslomopls") * ((Player.AuxVarGetFlt "*_ARnumofenm") - 1)) (begin function {}
			sgtm 1.0
		end)
	endif
end)

SetGameMainLoopCallback (begin function {}
	if eval !(Player.AuxVarGetFlt "*_ARappalert") && (Player.AuxVarGetFlt "*_ARnumofenm")
		DispatchEventAlt "AlertReactionStart"
	endif
end) 1 3 1