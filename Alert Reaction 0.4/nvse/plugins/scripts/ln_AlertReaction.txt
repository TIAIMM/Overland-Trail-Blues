StopSoundFile
UnloadUIComponent "HUDMainMenu\AlertReaction\AM1"

if eval GetNVSEVersionFull < 6.35
	MessageBoxEx "Alert Reaction requirement not detected!%rInstall latest xNVSE."
	return
endif
if eval GetPluginVersion "JIP LN NVSE" < 57.30
	MessageBoxEx "Alert Reaction requirement not detected!%rInstall latest JIP LN NVSE Plugin."
	return
endif
if eval GetPluginVersion "JohnnyGuitarNVSE" < 511
	MessageBoxEx "Alert Reaction requirement not detected!%rInstall latest JohnnyGuitar NVSE."
	return
endif
if eval GetPluginVersion "ShowOffNVSE Plugin" < 180
	MessageBoxEx "Alert Reaction requirement not detected!%rInstall latest ShowOff NVSE plugin."
	return
endif
if GetPluginVersion "UI Organizer Plugin" < 230
	MessageBoxEx "Alert Reaction requirement not detected!%rInstall latest UIO - User Interface Organizer."
	return
endif

Player.AuxVarSetFlt "*_ARearlyfeh" (GetMaxOf 0 (GetMinOf 1 (GetINIFloat "Global:fAlertReactionEarlierRefresh" "AlertReaction.ini")))

Player.AuxVarSetFlt "*_ARaltsotog" (GetMaxOf 0 (GetMinOf 1 (GetINIFloat "AlertSound:fAlertSoundToggle" "AlertReaction.ini")))
Player.AuxVarSetFlt "*_ARaltsorag" (GetMaxOf 0 (GetINIFloat "AlertSound:fAlertSoundRange" "AlertReaction.ini"))
Player.AuxVarSetFlt "*_ARaltsonsk" (GetMaxOf 0 (GetMinOf 1 (GetINIFloat "AlertSound:fAlertSoundWhenNotSneaking" "AlertReaction.ini")))

Player.AuxVarSetFlt "*_ARslomotog" (GetMaxOf 0 (GetMinOf 1 (GetINIFloat "SlowMotion:fSlowMotionToggle" "AlertReaction.ini")))
Player.AuxVarSetFlt "*_ARslomorag" (GetMaxOf 0 (GetINIFloat "SlowMotion:fSlowMotionRange" "AlertReaction.ini"))
Player.AuxVarSetFlt "*_ARslomonsk" (GetMaxOf 0 (GetMinOf 1 (GetINIFloat "SlowMotion:fSlowMotionWhenNotSneaking" "AlertReaction.ini")))
Player.AuxVarSetFlt "*_ARslomosot" (GetMaxOf 0 (GetMinOf 1 (GetINIFloat "SlowMotion:fSlowMotionSoundToggle" "AlertReaction.ini")))
Player.AuxVarSetFlt "*_ARslomosca" (GetMaxOf 0 (GetINIFloat "SlowMotion:fSlowMotionTimeScale" "AlertReaction.ini"))
Player.AuxVarSetFlt "*_ARslomodur" (GetMaxOf 0 (GetINIFloat "SlowMotion:fSlowMotionDuration" "AlertReaction.ini"))
Player.AuxVarSetFlt "*_ARslomopls" (GetMaxOf 0 (GetINIFloat "SlowMotion:fSlowMotionEnemyNumGain" "AlertReaction.ini"))

Player.AuxVarSetFlt "*_ARaltmktog" (GetMaxOf 0 (GetMinOf 1 (GetINIFloat "AlertMarker:fAlertMarkertoggle" "AlertReaction.ini")))
Player.AuxVarSetFlt "*_ARaltmksiz" (GetMaxOf 0 (GetINIFloat "AlertMarker:fAlertMarkerSizeScale" "AlertReaction.ini"))
Player.AuxVarSetFlt "*_ARaltmkrag" (GetMaxOf 0 (GetINIFloat "AlertMarker:fAlertMarkerRange" "AlertReaction.ini"))
Player.AuxVarSetFlt "*_ARaltmknsk" (GetMaxOf 0 (GetMinOf 1 (GetINIFloat "AlertMarker:fAlertMarkerWhenNotSneaking" "AlertReaction.ini")))
Player.AuxVarSetFlt "*_ARaltmkcor" (GetMaxOf 0 (GetMinOf 255 (GetINIFloat "AlertMarker:fAlertMarkerColorRed" "AlertReaction.ini")))
Player.AuxVarSetFlt "*_ARaltmkcog" (GetMaxOf 0 (GetMinOf 255 (GetINIFloat "AlertMarker:fAlertMarkerColorGreen" "AlertReaction.ini")))
Player.AuxVarSetFlt "*_ARaltmkcob" (GetMaxOf 0 (GetMinOf 255 (GetINIFloat "AlertMarker:fAlertMarkerColorBlue" "AlertReaction.ini")))

Player.AuxVarSetFlt "*_ARappalert" 0
Player.AuxVarSetFlt "*_ARnumofenm" 0

Player.AuxVarSetFlt "*_ARtilesnum" 0
Player.AuxVarSetFlt "*_ARisonscrn" 0
Player.AuxVarSetFlt "*_ARscraxisx" 0
Player.AuxVarSetFlt "*_ARscraxisy" 0
Player.AuxVarSetFlt "*_ARscraxisz" 0

SetUIFloatAlt "HudMainMenu\AlertReaction\_MarkerRed" (Player.AuxVarGetFlt "*_ARaltmkcor")
SetUIFloatAlt "HudMainMenu\AlertReaction\_MarkerGreen" (Player.AuxVarGetFlt "*_ARaltmkcog")
SetUIFloatAlt "HudMainMenu\AlertReaction\_MarkerBlue" (Player.AuxVarGetFlt "*_ARaltmkcob")

;Update enemy number and save first enemy as a ref
SetEventHandlerAlt "OnStartCombat" (begin function {ref rTrt, ref rAtk}
	if eval rTrt
		Player.AuxVarSetFlt "*_ARnumofenm" (Ar_Size Player.GetCombatTargets)
		Player.AuxVarSetRef "*_ARcombatenm" rTrt
	endif
end) 2::player

SetEventHandlerAlt "DebugLocationPrint" (begin function {ref rARce}
	if rARce
		print "Z:" + (tostring (rARce).GetObjectDimensions Z)
		print "XLoc:" + (tostring rARce.GetPos X)
		print "YLoc:" + (tostring rARce.GetPos Y)
		print "ZLoc:" + (tostring rARce.GetPos Z)
	endif
end)

SetEventHandlerAlt "ScreenLocationCal" (begin function {ref rARco}
	if rARco
		Player.AuxVarSetFlt "*_ARisonscrn" (WorldToScreen (float xOut) (float yOut) (float zOut) 0 0 (rARco.GetObjectDimensions Z) 0 rARco)
		print "OnScreen:" + (tostring (Player.AuxVarGetFlt "*_ARisonscrn"))
		Player.AuxVarSetFlt "*_ARscraxisx" xOut
		Player.AuxVarSetFlt "*_ARscraxisy" yOut
		Player.AuxVarSetFlt "*_ARscraxisz" zOut
		printvar xOut,yOut,zOut
		DispatchEventAlt "AlertMarkerTileCal"
	endif
end)

SetEventHandlerAlt "AlertMarkerTileCal" (begin function {}
	SetUIFloatAlt "HudMainMenu\AlertReaction\_MarkerX" (Player.AuxVarGetFlt "*_ARscraxisx")
	SetUIFloatAlt "HudMainMenu\AlertReaction\_MarkerY" (Player.AuxVarGetFlt "*_ARscraxisy")
	SetUIFloatAlt "HudMainMenu\AlertReaction\_MarkerZ" ((Player.AuxVarGetFlt "*_ARaltmksiz") * (Player.AuxVarGetFlt "*_ARscraxisz"))
	DispatchEventAlt "AlertMarkerTileAdd"
end)

SetEventHandlerAlt "AlertMarkerTileAdd" (begin function {}
	AddTileFromTemplate "HUDMainMenu\AlertReaction|AlertMarker|AM1"
end)

SetEventHandlerAlt "AlertReactionOnFilter" (begin function {}
	if eval (Player.AuxVarGetFlt "*_ARearlyfeh")
		DispatchEventAlt "AlertReactionEarlierStopFilter"
	else
		DispatchEventAlt "AlertReactionStopFilter"
	endif
end)

SetEventHandlerAlt "AlertReactionEarlierStopFilter" (begin function {}
	if (Player.AuxVarGetRef "*_ARcombatenm")
		if eval (GetPCDetectionState != 3) && !((Player.AuxVarGetRef "*_ARcombatenm").IsLimbCrippled 6)
			DispatchEventAlt "ClearAlertReactionEffect"
		endif
	else
		if eval (GetPCDetectionState != 3)
			DispatchEventAlt "ClearAlertReactionEffect"
		endif
	endif
end)

SetEventHandlerAlt "AlertReactionStopFilter" (begin function {}
	if (Player.AuxVarGetRef "*_ARcombatenm")
		if eval !(Player.IsInCombat) && !((Player.AuxVarGetRef "*_ARcombatenm").IsLimbCrippled 6)
			DispatchEventAlt "ClearAlertReactionEffect"
		endif
	else
		if eval !(Player.IsInCombat)
			DispatchEventAlt "ClearAlertReactionEffect"
		endif
	endif
end)

;Clear effect apply status,enemy number and stop sound/slowmo
SetEventHandlerAlt "ClearAlertReactionEffect" (begin function {}
	Player.AuxVarSetFlt "*_ARappalert" 0
	Player.AuxVarSetFlt "*_ARnumofenm" 0
	Player.AuxVarErase "*_ARcombatenm"
	sgtm 1.0
	StopSoundFile
	UnloadUIComponent "HUDMainMenu\AlertReaction\AM1"
end)

;Change effect apply status
SetEventHandlerAlt "AlertReactionStart" (begin function {}
	Player.AuxVarSetFlt "*_ARappalert" 1
	DispatchEventAlt "AlertReactionAlertSoundFilter"
	DispatchEventAlt "AlertReactionSlowMoFilter"
	DispatchEventAlt "DebugLocationPrint" (Player.AuxVarGetRef "*_ARcombatenm")
	DispatchEventAlt "ScreenLocationCal" (Player.AuxVarGetRef "*_ARcombatenm")
end)

; Check range, sneaking status, INI settings before execute alert sound
SetEventHandlerAlt "AlertReactionAlertSoundFilter" (begin function {}
	if eval (Player.AuxVarGetRef "*_ARcombatenm")
		if eval Player.GetDistance3D (Player.AuxVarGetRef "*_ARcombatenm") <= (Player.AuxVarGetFlt "*_ARaltsorag") && (player.IsSneaking || (Player.AuxVarGetFlt "*_ARaltsonsk")) && Player.AuxVarGetFlt "*_ARaltsotog"
				PlaySoundFile "Data/sound/fx/AlertReaction/AlertSound.mp3" 1 0 0
		endif
	endif
end)

; Check range, sneaking status, INI settings before execute slowmo
SetEventHandlerAlt "AlertReactionSlowMoFilter" (begin function {}
	if eval (Player.AuxVarGetRef "*_ARcombatenm")
		if eval Player.GetDistance3D (Player.AuxVarGetRef "*_ARcombatenm") <= (Player.AuxVarGetFlt "*_ARslomorag") && (player.IsSneaking || (Player.AuxVarGetFlt "*_ARslomonsk")) && Player.AuxVarGetFlt "*_ARslomotog" && Player.AuxVarGetFlt "*_ARslomosca"
			DispatchEventAlt "AlertReactionSlowMo"
		endif
	endif
end)

; SlowMo Time Calculation: Duration * Timescale + EnemyNumGain * (EnemyNumber - 1)
SetEventHandlerAlt "AlertReactionSlowMo" (begin function {}
	if eval ggtm == 1.0
		sgtm (Player.AuxVarGetFlt "*_ARslomosca")
		if eval Player.AuxVarGetFlt "*_ARslomosot"
			DispatchEventAlt "AlertReactionSlowMoSound"
		endif
		CallAfterSeconds ((Player.AuxVarGetFlt "*_ARslomodur") * (Player.AuxVarGetFlt "*_ARslomosca") + (Player.AuxVarGetFlt "*_ARslomopls") * ((Player.AuxVarGetFlt "*_ARnumofenm") - 1)) (begin function {}
			sgtm 1.0
			StopSoundFile
		end)
	endif
end)

; SlowMo Sound Delay Time Calculation: 1.5 * Timescale
SetEventHandlerAlt "AlertReactionSlowMoSound" (begin function {}
	if eval (Player.AuxVarGetFlt "*_ARaltsotog") && ((Player.AuxVarGetFlt "*_ARslomodur") >= 1.5)
		CallAfterSeconds ( 1.5 * (Player.AuxVarGetFlt "*_ARslomosca")) ({} => PlaySoundFile "Data/sound/fx/AlertReaction/Heartbeatsound.mp3" 1 1 0)
	endif
	if eval !(Player.AuxVarGetFlt "*_ARaltsotog")
		PlaySoundFile "Data/sound/fx/AlertReaction/Heartbeatsound.mp3" 1 1 0
	endif
end)

;Execute alert reaction by checking combat enemy number and whether the effect apply status every frame
SetGameMainLoopCallback (begin function {}
	if eval !(Player.AuxVarGetFlt "*_ARappalert") && (Player.AuxVarGetFlt "*_ARnumofenm")
		DispatchEventAlt "AlertReactionStart"
	elseif (Player.AuxVarGetFlt "*_ARappalert")
		DispatchEventAlt "AlertReactionOnFilter"
	endif
end) 1 3 1